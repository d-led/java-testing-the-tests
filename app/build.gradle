/*
 * SPDX-FileCopyrightText: Â© 2024 Siemens AG
 * SPDX-License-Identifier: Unlicense
 */

plugins {
    // Apply the application plugin to add support for building a CLI application in Java.
    id 'application'
    id 'info.solidsoft.pitest' version '1.15.0'
}

repositories {
    // Use Maven Central for resolving dependencies.
    mavenCentral()
}

ext {
    jqwikVersion = '1.7.4'
    junitVersion = '5.9.3'
    assertjVersion = '3.24.2'
}

compileTestJava {
    // To enable argument names in reporting and debugging
	options.compilerArgs += '-parameters'
}

dependencies {
    testImplementation "org.junit.jupiter:junit-jupiter:${junitVersion}"
    testImplementation "org.assertj:assertj-core:${assertjVersion}"
    testImplementation "net.jqwik:jqwik:${jqwikVersion}"
    pitest 'org.pitest:pitest-rv-plugin:0.1'
}

application {
    // Define the main class for the application.
    mainClass = 'com.siemens.code.dled.App'
}

tasks.named('test') {
    // Use JUnit Platform for unit tests.
    useJUnitPlatform()
}

pitest {
    targetClasses = ['com.siemens.*']
    mutators = [
        "STRONGER",
        "AOR" // https://pitest.org/quickstart/mutators/#EXPERIMENTAL_AOR
    ]
    pitestVersion = "1.15.6"
    outputFormats = ['HTML', 'XML']
    timestampedReports = false
    verbose = true
    junit5PluginVersion = '1.2.1' // https://mvnrepository.com/artifact/org.pitest/pitest-junit5-plugin
    excludedTestClasses = [ // for demonstration purposes
        'com.siemens.**.*Properties'
    ]
}

test {
	useJUnitPlatform {
		includeEngines 'jqwik', 'junit-jupiter'
		// includeTags 'fast', 'medium'
		// excludeTags 'slow'
	}

	// include '**/*Properties.class'
	// include '**/*Test.class'
	// include '**/*Tests.class'
}

tasks.withType(Test) {
    testLogging {
        exceptionFormat "full"
        events 'standard_out' //, 'standard_error'
        // showStandardStreams = true // enable to show full, but noisier output
    }

    afterTest { descr, result ->
        def elapsed = result.endTime - result.startTime
        println ("$descr took $elapsed ms.")
    }
}
